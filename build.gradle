plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'com.github.getplusm'
    version = '2.4.7'
    sourceCompatibility = 17
    targetCompatibility = 17

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        // Spigot API * start
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

            // As of Gradle 5.1, you can limit this to only those
            // dependencies you expect from it
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
        maven { url = 'https://repo.dmulloy2.net/repository/public/' }
        maven { url = 'https://oss.sonatype.org/content/groups/public/' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://oss.sonatype.org/content/repositories/central' }
        maven { url = 'https://repo.codemc.org/repository/maven-public/' }
        // Spigot API * end

        // Paper API
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }

        // more & more
        maven { url = 'https://jitpack.io' }

        // floadgate api
        maven { url = 'https://repo.opencollab.dev/maven-snapshots/' }

        // PlasmoVoice
        maven { url = 'https://repo.plo.su/' }

        // WorldGuard
        maven { url = 'https://maven.enginehub.org/repo/' }
        // PAPI
        maven {
            url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
        }
    }
}

dependencies {
    //implementation(project(path: ":engine"))
    //implementation(project(path: ":folia"))
    implementation project(':engine')
    implementation project(':folia')
}

tasks {

    shadowJar {
        archivesBaseName = "PLAZMER-ENGINE"
        archiveClassifier.set("")
        destinationDirectory.set(rootProject.buildDir)
    }

    build {
        dependsOn(shadowJar)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadowJar) {
                classifier = null
            }
            groupId = "com.github.getplusm"
            artifactId = "PLAZMER-ENGINE"
            version = project.version

            pom {
                name.set("PLAZMER-ENGINE")
                description.set("Plugin Engine")
                url.set("https://github.com/getplusm/Engine")
            }
        }
    }
    repositories {
        maven {
            name = 'jitpack'
            url = 'https://jitpack.io'
        }
    }
}
